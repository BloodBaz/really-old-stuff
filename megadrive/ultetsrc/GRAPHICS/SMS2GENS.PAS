program Sms2gens;
	type
		SMSTile=array[0..7,0..3] of byte;
		GenTile=array[0..7,0..3] of byte;
		RawTile=array[0..7,0..7] of byte;
	var F:file;
		T:text;
		TileCount,Junk:integer;
		S:string;
	function HexByte(B:byte):string;
		const
			HexChar:array[$00..$0F] of char=('0','1','2','3',
											 '4','5','6','7',
											 '8','9','A','B',
											 'C','D','E','F');
		begin
			HexByte:=HexChar[B shr 4]+HexChar[B and $0F];
		end;
	procedure SMS2Raw(var Src:SMSTile;var Dst:RawTile);
		var i,j,k:byte;
		begin
			for i:=0 to 7 do
				for j:=0 to 7 do
					Dst[i,j]:=$00;
			for i:=0 to 7 do
				for j:=0 to 7 do
					for k:=3 downto 0 do
						begin
							Dst[i,j]:=Dst[i,j] shl 1;
							Dst[i,j]:=Dst[i,j] or (((Src[i,k] shl j) shr 7) and $01);
						end;
		end;
	procedure Raw2Gen(var Src:RawTile;var Dst:GenTile);
		var i,j,k:byte;
		begin
			for i:=0 to 7 do
				for j:=0 to 3 do
					begin
						Dst[i,j]:=Src[i,(j*2)] and $0F;
						Dst[i,j]:=Dst[i,j] shl 4;
						Dst[i,j]:=Dst[i,j] or Src[i,(j*2)+1] and $0F;
					end;
		end;
	procedure ConvTile;
		const
			ConvTabl:array[0..3] of byte=($00,$01,$10,$11);
		var SMS:SMSTile;
			Gen:GenTile;
			Raw:RawTile;
			i,j,k:byte;
		begin
			BlockRead(F,SMS,SizeOf(SMS));
			{Primeiro, limpa o caracter}
			for i:=0 to 7 do
				for j:=0 to 3 do
					Gen[i,j]:=$00;
			{Agora, traduz os formatos}
			SMS2Raw(SMS,Raw);
			RAW2Gen(Raw,Gen);
			{Finalmente, escreve no arquivo}
			for i:=0 to 7 do
				begin
					Write(T,#09,'dc.l $');
					for j:=0 to 3 do
						Write(T,HexByte(Gen[i,j]));
					Writeln(T);
				end;
		end;
Begin
	if ParamCount < 3 then
		begin
			Writeln('SMS2GENS v1.0 by Haroldo de Oliveira Pinheiro');
			Writeln('Usage: SMS2GENS <tilecount> <infile> <outfile>');
			Halt(1);
		end;

	Val(ParamStr(1),TileCount,Junk);
	Assign(F,ParamStr(2));
	Reset(F,1);
	Assign(T,ParamStr(3));
	Rewrite(T);

	Writeln(T,'; Generated by SMS2GENS v1.0');
	Writeln(T);

	S:='_'+ParamStr(2);
	for Junk:=1 to Length(S) do
		begin
			if S[Junk] in ['.','-'] then
				S[Junk]:='_';
		end;
	Writeln(T,#09,'.globl ',S);
	Writeln(T,S,':');

	for Junk:=0 to Pred(TileCount) do
		begin
			if Junk <> 0 then
				begin
					Writeln(T);
					Writeln(T,'* --------------------');
				end;
			ConvTile;
		end;

	Close(F);
	Close(T);
End.